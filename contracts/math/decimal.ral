Abstract Contract Decimal() {
    fn getMaxTick(tickSpacing: U256) -> I256 {
        let convertedTickSpacing = toI256!(tickSpacing)
        return GLOBAL_MAX_TICK / convertedTickSpacing * convertedTickSpacing
    }

    fn getMinTick(tickSpacing: U256) -> I256 {
        let convertedTickSpacing = toI256!(tickSpacing)
        return GLOBAL_MIN_TICK / convertedTickSpacing * convertedTickSpacing
    }

    fn mul(l: U256, r:U256, rScale: U256) -> U256 {
        return (l * r) / rScale
    }

    fn mulUp(l: U256, r: U256, rScale: U256) -> U256 {
        return (l * r + rScale - 1) / rScale
    } 

    fn almostOne(scale: U256) -> U256 {
        return 10 ** scale - 1
    }

    fn one(scale: U256) -> U256 {
        return 10 ** scale
    }

    fn calculateSqrtPrice(
        tickIndex: I256
    ) -> SqrtPrice {
        let mut tickIndexAbs = 0
        if (tickIndex < 0i) {
            tickIndexAbs = toU256!((-1) * tickIndex)
        } else {
            tickIndexAbs = toU256!(tickIndex)
        }

        let mut sqrtPrice = FixedPoint{v: FIXED_POINT_DENOMINATOR}

        assert!(tickIndexAbs <= toU256!(GLOBAL_MAX_TICK), DecimalError.TickOverBounds)

        if (tickIndexAbs & 0x00001 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1000049998750062496094023) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x00002 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1000100000000000000000000) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x00004 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1000200010000000000000000) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x00008 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1000400060004000100000000) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x00010 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1000800280056007000560028)  / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x00020 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1001601200560182043688009) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x000040 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1003204964963598014666528) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x00080 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1006420201727613920156533) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x00100 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1012881622445451097078095) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x00200 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1025929181087729343658708) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x00400 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1052530684607338948386589) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x00800 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1107820842039993613899215) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x01000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1227267018058200482050503) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x02000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1506184333613467388107955) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x04000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 2268591246822644826925609) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x08000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 5146506245160322222537991) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x10000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 26486526531474198664033811) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x20000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 701536087702486644953017488) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x40000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 492152882348911033633683861778) / FIXED_POINT_DENOMINATOR
        }
        if (tickIndexAbs & 0x80000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 242214459604341065650571799093539783) / FIXED_POINT_DENOMINATOR
        }

        if (tickIndex >= 0i) {
            return SqrtPrice{ v: rescale(sqrtPrice.v, FIXED_POINT_SCALE, SQRT_PRICE_SCALE)}
        } else {
            let sqrtPriceInFixedPointScale = FIXED_POINT_DENOMINATOR * FIXED_POINT_DENOMINATOR / sqrtPrice.v
            return SqrtPrice{ v: rescale(sqrtPriceInFixedPointScale, FIXED_POINT_SCALE, SQRT_PRICE_SCALE)}
        }
    }

    fn rescale(fromValue: U256, fromScale: U256, expectedScale: U256) -> U256 {
        if (expectedScale > fromScale) {
            let multiplierScale = expectedScale - fromScale
            return fromValue * (10 ** multiplierScale)
        } else {
            let denominatorScale = fromScale - expectedScale
            return fromValue / (10 ** denominatorScale)
        }
    }

    fn wrappingAdd(a: U256, b: U256) -> U256 {
        if (b > MAX_U256 - a) {
            return (b - (MAX_U256 - a)) - 1
        } else {
            return a + b
        }
    }

    fn wrappingSub(a: U256, b: U256) -> U256 {
        if (b > a) {
            return MAX_U256 - (b - a) + 1
        } else {
            return a - b
        }
    }

    fn calculateFeeGrowthInside(
        tickLowerIndex: I256,
        tickLowerFeeGrowthOutsideX: FeeGrowth,
        tickLowerFeeGrowthOutsideY: FeeGrowth,
        tickUpperIndex: I256,
        tickUpperFeeGrowthOutsideX: FeeGrowth,
        tickUpperFeeGrowthOutsideY: FeeGrowth,
        tickCurrent: I256,
        globalFeeGrowthX: FeeGrowth,
        globalFeeGrowthY: FeeGrowth
    ) -> (FeeGrowth,FeeGrowth) {
        let currentAboveLower = tickCurrent >= tickLowerIndex
        let currentBelowUpper = tickCurrent < tickUpperIndex

        let mut feeGrowthBelowX = FeeGrowth{v: 0}
        let mut feeGrowthBelowY = FeeGrowth{v: 0}
        
        let mut feeGrowthAboveX = FeeGrowth{v: 0}
        let mut feeGrowthAboveY = FeeGrowth{v: 0}

        if (currentAboveLower) {
            feeGrowthBelowX = tickLowerFeeGrowthOutsideX
            feeGrowthBelowY = tickLowerFeeGrowthOutsideY
        } else {
            feeGrowthBelowX.v = wrappingSub(globalFeeGrowthX.v, tickLowerFeeGrowthOutsideX.v)
            feeGrowthBelowY.v = wrappingSub(globalFeeGrowthY.v, tickLowerFeeGrowthOutsideY.v)
        }

        if (currentBelowUpper) {
            feeGrowthAboveX = tickUpperFeeGrowthOutsideX
            feeGrowthAboveY = tickUpperFeeGrowthOutsideY
        } else {
            feeGrowthAboveX.v = wrappingSub(globalFeeGrowthX.v, tickUpperFeeGrowthOutsideX.v)
            feeGrowthAboveY.v = wrappingSub(globalFeeGrowthY.v, tickUpperFeeGrowthOutsideY.v)
        }

        let feeGrowthInsideX = FeeGrowth{v: wrappingSub(wrappingSub(globalFeeGrowthX.v, feeGrowthBelowX.v), feeGrowthAboveX.v)}
        let feeGrowthInsideY = FeeGrowth{v: wrappingSub(wrappingSub(globalFeeGrowthY.v, feeGrowthBelowY.v), feeGrowthAboveY.v)}

        return feeGrowthInsideX, feeGrowthInsideY
    }
}
